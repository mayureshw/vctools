:-import
    unparse/1
    from unparsecpp.

:-import
    member/2,
    nth/3
    from lists.

:-import
    unparse/1
    from unparsecpp.

:-import
    map/3
    from utils.

%%%%%%%%%%% Operator related %%%%%%%%%%

% operators identified by vC class
opcls( 'Assign',  vcInterlockBuffer ).
opcls( 'Phi',     vcPhi             ).
opcls( 'Phi',     vcPhiPipelined    ).
opcls( 'Branch',  vcBranch          ).
opcls( 'Assign',  vcEquivalence     ).
opcls( 'Load',    vcLoad            ).
opcls( 'Store',   vcStore           ).
opcls( 'Inport',  vcInport          ).
opcls( 'Outport', vcOutport         ).
opcls( 'Call',    vcCall            ).
opcls( 'Select',  vcSelect          ).
opcls( 'Slice',   vcSlice           ).

extracls( vcBinarySplitOperator ).
extracls( vcUnarySplitOperator  ).

% operators identified by vC operator
opop( 'Plus',   '+'  ).
opop( 'Minus',  '-'  ).
opop( 'Mult',   '*'  ).
opop( 'Lt',     '<'  ).
opop( 'Gt',     '>'  ).
opop( 'Ge',     '>=' ).
opop( 'Ne',     '!=' ).
opop( 'Eq',     '==' ).
opop( 'And',    '&'  ).
opop( 'Or',     '|'  ).
opop( 'Not',    '~'  ).
opop( 'Bitsel', '[]' ).
opop( 'Concat', '&&' ).

subtyps( bool,  [ uint8_t ] ).
subtyps( uint,  [ uint8_t, uint16_t, uint32_t, uint64_t ] ).
subtyps( wuint, [ wuint ] ).
subtyps( float, [ float, double ] ).

nonFloatOrder([ uint8_t, uint16_t, uint32_t, uint64_t, wuint ]).

sign(O, [Num,Num,Num]    ) :- member( O, ['Plus','Minus','Mult'] ),    !, numeric(Num).
sign(O, [Bool,Num,Num]   ) :- member( O, ['Lt','Gt','Ge','Ne','Eq'] ), !, bool(Bool), numeric(Num).
sign(O, [NF,NF,NF]       ) :- member( O, ['And','Or'] ),               !, nofloat(NF).
sign(O, [Any]            ) :- member( O, ['Inport','Outport'] ),       !, any(Any).
sign('Not',    [Bool,Bool]        ):-!, bool(Bool).
sign('Assign', [T1,T2]            ):-!, ( nofloat(T1), nofloat(T2) ) ; ( floaT(T1), floaT(T2) ).
sign('Phi',    [Any,Any,Any]      ):-!, any(Any).
sign('Branch', [Bool]             ):-!, bool(Bool).
sign('Load',   [Any,UInt]         ):-!, any(Any), uint(UInt).
sign('Store',  [UInt,Any]         ):-!, uint(UInt), any(Any).
sign('Select', [Any,Bool,Any,Any] ):-!, any(Any), bool(Bool).
sign('Bitsel', [Bool,NF,UInt]     ):-!, bool(Bool), nofloat(NF), uint(UInt).
sign('Concat', [NF1,NF2,NF3]      ):-!, nofloat(NF1), nofloat(NF2), nofloat(NF3), nfge(NF1,NF2), nfge(NF1,NF3).
sign('Slice',  [NF1,NF2]          ):-!, nofloat(NF1), nofloat(NF2), nfge(NF2,NF1).
sign('Call',   []                 ):-!.

numeric(ST) :- ( T = uint; T = float ), subtyp(T, ST).
nofloat(ST) :- ( T = uint; T = wuint ), subtyp(T, ST).
floaT(ST)   :- subtyp(float, ST).
uint(ST)    :- subtyp(uint, ST).
any(ST)     :- subtyp(_, ST).
bool(ST)    :- subtyp(bool, ST).

creator( O,       'createIOPort'  ):- member( O, [ 'Inport', 'Outport' ] ), !.
creator( 'Load',  'createLoad'    ):-!.
creator( 'Store', 'createStore'   ):-!.
creator( 'Call',  'createCall'    ):-!.
creator( 'Slice', 'createSlice'   ):-!.
creator( _,       'createGeneral' ).

%%%%%%%%%% Pipe related %%%%%%%%%%

pifotyp('Lifo').
pifotyp('Fifo').

pblktyp('BlockingPipe').
pblktyp('NonBlockingPipe').
pblktyp('SignalPipe').

%%%%%%%%%%

vcid(O, VCID) :- opcls(O, VCID) ; opop(O, VCID).

vctyp( T ) :- opcls(_, T); extracls( T ).
vctypsuf( T ) :- vctyp( T1 ), suf_( T1, T ).
vctypmap( br( [ str(T), Enum ] ) ) :- vctyp(T), suf_(T, Enum).

suf_(T, TSuf) :- str_cat(T, '_', TSuf).
suf_L(L, Lsuf) :- map(L, (A,B,suf_(A,B)), Lsuf).

:-table nfge/2.
nfge( NF1, NF2 ) :- nonFloatOrder(L), nth(N1, L, NF1), nth(N2, L, NF2), N1 >= N2.

:-table subtyp/2.
subtyp(T, ST) :- subtyps(T, STs), member(ST, STs).

:-table ctyp/1.
ctyp(ST) :- subtyp(_,ST1), suf_(ST1, ST).

opfmapv( O, Sig, lambda([this], 'OPRET', [ 'DPEARG' ], [Stmt]) ) :-
    CTyp = typ(O, Sig, ''), creator(O, Fcr),
    Stmt = stmt(ret(call(Fcr,[CTyp],[dpe]))).

opfmapk( O, Sig, br( [ str(VCID), br(Sig_) ] ) ) :-
    vcid(O, VCID), sign(O, Sig), suf_L(Sig, Sig_).

opfmapkv( br( [ K, V ] ) ) :- opfmapk(O, Sig, K), opfmapv(O, Sig, V).

opfmap(br(L)) :- findall(V, opfmapkv(V), L).

pfmapv(C, F, B, lambda( [], 'Pipe*', Params, Stmts ) ) :-
    Params = [ tv(int,[depth]), tv(int,[width]), tv(string,[label]) ],
    Stmts = [ stmt( ret( new( typ(F,[C,B],''), [depth, width, label] ) ) ) ].
pfmapk(C, F, B, br(L)) :- suf_L([C,F,B],L).
pfmapkv( br( [K, V] ) ) :- nofloat(C), pifotyp(F), pblktyp(B), pfmapk(C,F,B,K), pfmapv(C,F,B,V).
pfmap(br(L)) :- findall(V, pfmapkv(V), L).

cpp('#ifndef _OPF_H').
cpp('#define _OPF_H').

% TODO: wuint defined temporarily
cpp(def('wuint','unsigned long')).
cpp(def('CTYPENUM',br(L))) :- findall(TSuf, ctyp(TSuf), L1), sort(L1, L).
cpp(stmt(typdef('VCtyp',enum(L)))) :- findall(TSuf, vctypsuf(TSuf), L1), sort(L1, L).
cpp(def('VCTYPMAP',br(L))) :- findall(V, vctypmap(V), L).
cpp(def('OPRET',typ('Operator',[],'*'))).
cpp(def('DPEARG',tv(typ('vcDatapathElement',[],'*'), [dpe]))).
cpp(def('OPFMAP',Cpp)) :- opfmap(Cpp).

cpp(def('PIFOENUM',br(L))) :- findall(Pifo, pifotyp(Pifo), L1), suf_L(L1,L).
cpp(def('PBLKENUM',br(L))) :- findall(Pifo, pblktyp(Pifo), L1), suf_L(L1,L).
cpp(def('PFMAP',Cpp)) :- pfmap(Cpp).

cpp('#endif').

%%% Test drivers
:- cpp(CPP), unparse(CPP), fail.
